import org.gradle.internal.os.OperatingSystem

import java.nio.file.Files
import java.nio.file.Paths

apply plugin: 'maven-publish'

configurations {
    archives
}

publishing {
    repositories {
        maven {
            url "${System.getProperty('user.home')}/releases/maven/development"
        }
        maven {
            url "${System.getProperty('user.home')}/releases/maven/release"
        }
    }
}

def getTriplet() {
    def triplet
    def os_name = System.getProperty("os.name")
    def os_arch = System.getProperty("os.arch")

    if (os_arch == 'amd64') {
        os_arch = 'x64'
    } else if (os_arch == 'i386') {
        os_arch = 'x86'
    } else if (os_arch == 'aarch64' || os_arch == 'arm64') {
        os_arch = 'arm64'
    }

    if (OperatingSystem.current().isWindows()) {
        triplet = "${os_arch}-windows-static-md"
    } else if (OperatingSystem.current().isLinux()) {
        triplet = "${os_arch}-linux"
    } else if (OperatingSystem.current().isMacOsX()) {
        triplet = "${os_arch}-osx"
    } else {
        triplet = "${os_arch}-${os_name}"
    }
    return triplet
}

def getPlatformPath(triplet) {
    if (triplet == "arm32-linux") {
        return "linux/arm32"
    } else if (triplet == "arm64-linux") {
        return "linux/arm64"
    } else if (triplet == "x64-linux") {
        return "linux/x86-64"
    } else if (triplet == "x64-osx") {
        return "osx/x86-64"
    } else if (triplet == "arm64-osx") {
        return "osx/arm64"
    } else if (triplet == "x64-windows-static-md") {
        return "windows/x86-64"
    } else if (triplet == "arm64-windows-static-md") {
        return "windows/arm64"
    } else {
        return ""
    }
}
def triplet
if (!project.hasProperty('platform')) {
    println "No 'platform' property specified; using the build system's platform"
    triplet = getTriplet()
} else {
    triplet = project.platform
}

if (project.hasProperty('forcealternatemacbuild')) {
    if (project.platform == 'arm64-osx') {
        triplet = 'x64-osx'
    } else if (ext.platform == 'x64-osx') {
        triplet = 'arm64-osx'
    }
}

ext.platformPath = getPlatformPath(triplet)
ext.platformClassifier = ext.platformPath.replaceFirst('/', '')

ext.repo = "thirdparty-ceres"
def pubVersion = "2.2-2"

def outputsFolder = file("$project.buildDir/outputs")

def baseArtifactId = 'ceres'
def artifactGroupId = 'edu.wpi.first.thirdparty.frc2024.ceres'
def zipBaseName = '_GROUP_edu_wpi_first_thirdparty_frc2024_ceres_ID_ceres-cpp_CLS'

def versionFile = file("$outputsFolder/version.txt")
def licenseFile = file("LICENSE.md")

def outputClassifierStatic = project.ext.platformClassifier + 'static'
println triplet

task copyAllOutputs(type: Copy) {
    destinationDir = outputsFolder
}

ext.addTaskToCopyAllOutputs = { task ->
    copyAllOutputs.dependsOn task
    copyAllOutputs.inputs.file task.archiveFile
    copyAllOutputs.from task.archiveFile
}

task buildVcpkg(type: Exec) {
    def baseArgs = ['install']
    outputs.dir "vcpkg_installed/$triplet"
    executable "$rootDir/vcpkg/vcpkg"
    args baseArgs + "--triplet=$triplet"
}

task outputVersions() {
    description = 'Prints the versions of this to a file for use by the downstream packaging project'
    group = 'Build'
    outputs.files(versionFile)

    doFirst {
        buildDir.mkdir()
        outputsFolder.mkdir()
    }

    doLast {
        versionFile.write pubVersion
    }
}

task cppHeadersZip(type: Zip) {
    dependsOn outputVersions
    destinationDirectory = outputsFolder
    archiveBaseName = zipBaseName
    archiveClassifier = "headers"

    from(licenseFile) {
        into '/'
    }
    def resolvedDir = Paths.get(projectDir.canonicalPath).resolve("vcpkg_installed").resolve(triplet).resolve("include")
    from(resolvedDir.toFile()) {
        into '/'
    }

    includeEmptyDirs = false
    dependsOn buildVcpkg
}

task cppLibsZipStatic(type: Zip) {
    destinationDirectory = outputsFolder
    archiveClassifier = outputClassifierStatic
    archiveBaseName = zipBaseName
    duplicatesStrategy = 'exclude'

    from(licenseFile) {
        into '/'
    }
    def resolvedDir = Paths.get(projectDir.canonicalPath).resolve("vcpkg_installed").resolve(triplet).resolve("lib")
    from(resolvedDir.toFile()) {
        into project.platformPath + '/static'
        exclude '**/pkgconfig'
        include '**/*.a'
        include '**/*.lib'
        include '**/*.pdb'
    }
    dependsOn buildVcpkg
}

task cppLibsZipStaticDebug(type: Zip) {
    destinationDirectory = outputsFolder
    archiveClassifier = outputClassifierStatic + 'debug'
    archiveBaseName = zipBaseName
    duplicatesStrategy = 'exclude'

    from(licenseFile) {
        into '/'
    }
    def resolvedDir = Paths.get(projectDir.canonicalPath).resolve("vcpkg_installed").resolve(triplet).resolve("debug").resolve("lib")
    from(resolvedDir.toFile()) {
        into project.platformPath + '/static'
        exclude '**/pkgconfig'
        include '**/*.a'
        include '**/*.lib'
        include '**/*.pdb'
    }
    dependsOn buildVcpkg
}

if (!project.hasProperty('skipRelease')) {
    addTaskToCopyAllOutputs(cppLibsZipStatic)
}

if (!project.hasProperty('skipDebug')) {
    addTaskToCopyAllOutputs(cppLibsZipStaticDebug)
}

model {
    publishing {
        publications {
            cpp(MavenPublication) {
                artifact cppHeadersZip
                artifact cppLibsZipStatic
                artifact cppLibsZipStaticDebug

                artifactId = "${baseArtifactId}-cpp"
                groupId artifactGroupId
                version pubVersion
            }
        }
        repositories {
            maven {
                url "${System.getProperty('user.home')}/releases/maven/${project.repo}"
            }
        }
    }
}
